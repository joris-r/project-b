component ::=
    ( 'MACHINE' | 'REFINEMENT' | 'IMPLEMENTATION' ) header statements 'END'
    
header ::=
    identifier paren_ident_list?
    
statements ::=
    ( 'REFINES' | 'CONCRETE_VARIABLES' ) ident_list
    |
    ( 'CONSTRAINTS' | 'INVARIANT' ) expression
    |
    'INITIALISATION' substitution
    |
    'OPERATIONS' operation_list
    
operation_list ::=
    operation ( ';' operation )*

operation ::=
    ( ident_list '<--' )? identifier paren_ident_list? '=' substitution
    
substitution ::=
    'BEGIN' substitution2 'END'
    |
    'PRE' expression 'THEN' substitution2 'END'
    |
    'VAR' ident_list 'IN' substitution2 'END'
    |
    'WHILE' expression
    'DO' substitution2
    'INVARIANT' expression
    'VARIANT' expression
    'END'
    
substitution2 ::=
    substitution2 ( ';' | '||' ) substitution2
    |
    expression ':=' expression
    |
    substitution

expression ::=
    ( '-' | 'not' ) expression
    |
    expression expression
    |
    ( expression ( '<=>' | '=>' ) )? expression2
    
expression2 ::= ( expression2 ( '&' | 'or' ) )? expression3

expression3 ::= ( expression3 ( ':' | '<=' | '=' ) )? expression4

expression4 ::= ( expression4 ( '-->' ) )? expression5

expression5 ::= ( expression5 ( '<|' | '\/' | '◦' | '×' ) )? expression6

expression6 ::= ( expression6 ( '..' ) )? expression7

expression7 ::= ( expression7 ( '+' | '-' ) )? expression8

expression8 ::= ( expression8 ( '·' | '/' | 'mod' ) )? term

term ::=
    identifier '$0'?
    |
    number
    |
    '(' expression ( ',' expression )* ')'
    |
    '{' expression '}'
    |
    '{' ident_list '|' expression '}'
    |
    '%' ( identifier | paren_ident_list )
        '.' '(' expression '|' expression ')'
    
    
paren_ident_list ::=
    '(' ident_list ')'

ident_list ::=
    identifier ( ',' identifier )*
    
identifier ::=
    letter ( letter | digit | '_' )*
    
number ::=
    digit+ ( '.' digit+ )?
